buildscript {
    ext {
        kotlin_version = '1.3.50'
        coroutine_version = '1.3.0-M2'
        bukkit_version = '1.12.2-SNAPSHOT'
    }
    repositories {
        maven {
            credentials {
                username "$nexusUser"
                password "$nexusPassword"
            }
            url 'http://nexus.readymaderepository.kro.kr/repository/maven-public/'
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven-publish'

group 'io.github.readymadeprogrammer'
version '3.2.3-SNAPSHOT'

repositories {
    maven {
        credentials {
            username "$nexusUser"
            password "$nexusPassword"
        }
        url 'http://nexus.readymaderepository.kro.kr/repository/maven-public/'
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    compileOnly "io.github.readymadeprogrammer:bloodbukkit-server:$bukkit_version"
    compileOnly("org.spigotmc:plugin-annotations:1.1.0-SNAPSHOT"){
        exclude group:'org.bukkit', module:'bukkit'
    }
    compile "io.github.microutils:kotlin-logging:1.5.4"
    compile "ch.qos.logback:logback-classic:1.2.3"
    compile "com.esotericsoftware.yamlbeans:yamlbeans:1.13"
    compile "com.github.salomonbrys.kotson:kotson:2.5.0"
    compile "com.github.ben-manes.caffeine:caffeine:2.6.2"
    compile "com.fatboyindustrial.gson-javatime-serialisers:gson-javatime-serialisers:1.1.1"
    compileOnly "com.comphenix.protocol:ProtocolLib-API:4.4.0"
    compile("com.comphenix.packetwrapper:PacketWrapper:1.12.2-SNAPSHOT"){
        exclude group:'org.bukkit', module:'bukkit'
    }
    compileOnly "io.github.readymadeprogrammer:SpikotClassLocator:3.2.0-SNAPSHOT"

    kapt 'org.spigotmc:plugin-annotations:1.1.0-SNAPSHOT'
    kapt "io.github.readymadeprogrammer:SpikotClassLocator:3.2.0-SNAPSHOT"
}

task fatJar(type: Jar) {
    baseName 'Spikot-shaded'
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += '-XXLanguage:+InlineClasses'
        freeCompilerArgs += '-Xuse-experimental=kotlin.Experimental'
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'Spikot'
            from components.java
//            artifact jar
        }
    }
    repositories {
        maven {
            credentials {
                username "$nexusUser"
                password "$nexusPassword"
            }

            if (version.endsWith('SNAPSHOT')) {
                url 'http://nexus.readymaderepository.kro.kr/repository/maven-snapshots/'
            } else {
                url 'http://nexus.readymaderepository.kro.kr/repository/maven-releases/'
            }
        }
    }
}