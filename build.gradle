plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.60"
    id "org.jetbrains.kotlin.kapt" version "1.3.60"
    id "org.jetbrains.dokka" version "0.10.0"
    id 'maven-publish'
}

def kotlin_version = '1.3.60'
def coroutine_version = '1.3.0-M2'
def paper_version = '1.12.2-R0.1-SNAPSHOT'

// Load local.gradle if exists
if (new File('local.gradle').exists())
    apply from: 'local.gradle'

group 'kr.heartpattern'
version '4.0.0-SNAPSHOT'

repositories {
    maven {
        url 'https://maven.heartpattern.kr/repository/maven-public/'
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += '-XXLanguage:+InlineClasses'
        freeCompilerArgs += '-Xuse-experimental=kotlin.Experimental'
    }
}

sourceSets {
    main.java.srcDirs += 'src/main/java'
    main.kotlin.srcDirs += 'src/main/kotlin'
    main.resources.srcDirs += 'src/main/resources'
}

dependencies {
    // Kotlin family
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    compile "io.github.microutils:kotlin-logging:1.5.4"
    compile "com.esotericsoftware.yamlbeans:yamlbeans:1.13"
    compile "com.github.salomonbrys.kotson:kotson:2.5.0"
    compile "com.github.ben-manes.caffeine:caffeine:2.6.2"
    compile "com.fatboyindustrial.gson-javatime-serialisers:gson-javatime-serialisers:1.1.1"
    compile 'net.jodah:typetools:0.6.1'

    // Compile only dependencies
    compileOnly 'com.google.auto.service:auto-service-annotations:1.0-rc6'
    compileOnly "com.comphenix.protocol:ProtocolLib:4.4.0"
    compileOnly "com.destroystokyo.paper:paper-api:$paper_version"
    compileOnly("org.spigotmc:plugin-annotations:1.1.0-SNAPSHOT") {
        exclude group: 'org.bukkit', module: 'bukkit'
    }

    // Test
    testCompile 'org.jetbrains.kotlin:kotlin-test:1.3.50'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.5.2'

    // KAPT
    kapt 'com.google.auto.service:auto-service:1.0-rc6'
    kapt 'org.spigotmc:plugin-annotations:1.1.0-SNAPSHOT'
    kapt "kr.heartpattern:SpikotClassLocator:4.0.0-SNAPSHOT"
    kapt "kr.heartpattern:SpikotAnnotationProcessor:4.0.0-SNAPSHOT"
}

test {
    useJUnitPlatform()
}

configurations {
    testImplementation.extendsFrom compileOnly
}

task createPlugin(type: Jar) {
    archiveName 'Spikot-Plugin.jar'
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

task createDokkaArchive(type: Zip){
    dependsOn dokka
    archiveFileName = "Spikot-kdoc.zip"
    destinationDirectory = file("./build/libs/")

    from "./build/kdoc"
}

dokka{
    outputFormat = 'html'
    outputDirectory = './build/kdoc/'
    configuration{
        externalDocumentationLink{
            url = new URL("https://hub.spigotmc.org/javadocs/spigot/")
            packageListUrl = new URL("https://hub.spigotmc.org/javadocs/spigot/package-list")
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'Spikot'
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                username "${nexusUser}"
                password "${nexusPassword}"
            }

            if (version.endsWith('SNAPSHOT')) {
                url 'https://maven.heartpattern.kr/repository/maven-public-snapshots/'
            } else {
                url 'https://maven.heartpattern.kr/repository/maven-public-releases/'
            }
        }
    }
}